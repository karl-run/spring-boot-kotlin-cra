buildscript {
    ext {
        kotlinVersion = '1.3.30'
        springBootVersion = '2.1.4.RELEASE'
    }
    repositories {
        mavenCentral()
        maven { url = "https://plugins.gradle.org/m2/" }
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
        classpath("org.jetbrains.kotlin:kotlin-gradle-plugin:${kotlinVersion}")
        classpath("org.jetbrains.kotlin:kotlin-allopen:${kotlinVersion}")
        classpath("org.junit.platform:junit-platform-gradle-plugin:1.2.0")
        classpath("com.moowork.gradle:gradle-node-plugin:1.2.0")
    }
}

apply plugin: 'kotlin'
apply plugin: 'kotlin-spring'
apply plugin: 'eclipse'
apply plugin: 'org.springframework.boot'
apply plugin: 'org.junit.platform.gradle.plugin'
apply plugin: 'io.spring.dependency-management'
apply plugin: 'com.moowork.node'

group = 'run.karl'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = 1.8
compileKotlin {
    kotlinOptions {
        freeCompilerArgs = ['-Xjsr305=strict']
        jvmTarget = '1.8'
    }
}
compileTestKotlin {
    kotlinOptions {
        freeCompilerArgs = ['-Xjsr305=strict']
        jvmTarget = '1.8'
    }
}

repositories {
    mavenCentral()
}

junitPlatform {
    filters {
        engines {
            include 'spek'
        }
    }
}

dependencies {
    implementation('org.springframework.boot:spring-boot-starter-web')
    implementation('com.graphql-java-kickstart:graphql-spring-boot-starter:5.4')
    implementation('com.graphql-java-kickstart:graphiql-spring-boot-starter:5.4')
    implementation('com.graphql-java-kickstart:graphql-java-tools:5.4.1')
    implementation('com.graphql-java-kickstart:voyager-spring-boot-starter:5.4')
    implementation('com.fasterxml.jackson.module:jackson-module-kotlin')
    implementation('org.jetbrains.kotlin:kotlin-stdlib-jdk8')
    implementation('org.jetbrains.kotlin:kotlin-reflect')
    testImplementation('org.springframework.boot:spring-boot-starter-test')
    testImplementation 'org.jetbrains.spek:spek-api:1.1.4'
    testImplementation 'com.natpryce:hamkrest:1.6.0.0'
    testImplementation 'com.nhaarman:mockito-kotlin:1.6.0'
    testRuntime 'org.jetbrains.spek:spek-junit-platform-engine:1.1.5'
}

task installDependencies(type: YarnTask) {
    execOverrides {
        it.workingDir = '../web'
    }
}

task buildWeb(type: YarnTask) {
    args = ['build:gradle']
    execOverrides {
        it.workingDir = '../web'
    }
}

buildWeb.dependsOn installDependencies
build.dependsOn buildWeb
